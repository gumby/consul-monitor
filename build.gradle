buildscript {
  repositories {
    jcenter()
  	mavenCentral()
  }
  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
  }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply plugin: 'com.google.protobuf'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

group 'com.cleo.clarify.chassis'
version '0.2.4'

shadowJar {
  baseName = 'com.cleo.clarify.chassis'
  mergeServiceFiles()
  relocate 'io.netty', 'internal.chassis.io.netty'
}

repositories {
  mavenCentral()
  jcenter()
}

def libraryGroupId = 'com.cleo.clarify'
def libraryArtifactId = 'chassis'
def libraryVersion = version

test {
    filter {
        includeTestsMatching "*AllTests"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier 'sources'
	from sourceSets.main.allSource
}
 
artifacts {
	archives sourcesJar
}

sourceSets {
  test {
    proto {
      srcDir 'src/test/proto'
    }
    java {
      srcDirs 'src/test/java', file("${protobuf.generatedFilesBaseDir}/test/java"), file("${protobuf.generatedFilesBaseDir}/test/grpc")
    }
  }
}

protobuf {
  protoc {
    // The version of protoc must match protobuf-java. If you don't depend on
    // protobuf-java directly, you will be transitively depending on the
    // protobuf-java version that grpc depends on.
    artifact = "com.google.protobuf:protoc:3.0.2"
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.0.1'
    }
  }
  
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

dependencies {
	compile 'com.google.inject:guice:4.0'
	compile 'com.google.inject.extensions:guice-servlet:4.0'
	
	compile 'javax.ws.rs:javax.ws.rs-api:2.1-m01'
	compile 'javax.servlet:javax.servlet-api:4.0.0-b01'

    compile 'org.eclipse.jetty:jetty-servlet:9.4.0.RC1'
    
    compile 'org.jboss.resteasy:resteasy-client:3.1.0.CR2'
    compile 'org.jboss.resteasy:resteasy-jaxrs:3.1.0.CR2'
    compile 'org.jboss.resteasy:resteasy-guice:3.1.0.CR2'
    compile 'org.jboss.resteasy:resteasy-jackson2-provider:3.1.0.CR2'
    
    compile 'com.typesafe:config:1.3.1'
    
    compile 'com.orbitz.consul:consul-client:0.13.2'
    
    compile 'io.dropwizard.metrics:metrics-healthchecks:3.1.2'
    
    compile 'io.grpc:grpc-netty:1.0.1'
	compile 'io.grpc:grpc-protobuf:1.0.1'
	compile 'io.grpc:grpc-stub:1.0.1'
	compile 'io.grpc:grpc-services:1.0.1'
	
	testCompile 'com.pszymczyk.consul:embedded-consul:0.2.0'
	testCompile 'junit:junit:4.12'
	testCompile 'com.jayway.awaitility:awaitility:1.7.0'
	testCompile 'com.github.tomakehurst:wiremock-standalone:2.3.1'
	testCompile 'org.mockito:mockito-core:1.+'
}